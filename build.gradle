plugins {
    id 'java-library'
    id 'maven-publish'
}

group 'zero.x.github.jmh.runner'
version '1.0.0'

repositories {
    mavenCentral()
}

dependencies {

    implementation 'org.openjdk.jmh:jmh-core:1.24'
    implementation 'org.openjdk.jmh:jmh-generator-annprocess:1.24'

    implementation 'com.google.guava:guava:29.0-jre'


    compile 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'

    testCompile 'org.junit.jupiter:junit-jupiter-api:5.7.0-M1'
    testCompile 'org.junit.jupiter:junit-jupiter-engine:5.7.0-M1'
    testCompileOnly 'org.projectlombok:lombok:1.18.12'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
}

test {
    // Use junit platform for unit tests
    useJUnitPlatform()

    // Always run tests, even when nothing changed.
    dependsOn 'cleanTest'

    // Show test results.
    testLogging {
        events "passed", "skipped", "failed"
    }
}


// https://docs.gradle.org/current/userguide/building_java_projects.html#sec:java_source_sets
sourceSets {
    jmh {
        java {
            srcDirs = ['src/jmh/java']
        }
        resources {
            srcDirs = ['src/jmh/resources']
        }
        compileClasspath += sourceSets.main.runtimeClasspath
        compileClasspath += sourceSets.test.runtimeClasspath
    }
}

dependencies {
    jmhAnnotationProcessor 'org.mapstruct:mapstruct-processor:1.3.1.Final'
    jmhAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
    jmhImplementation 'org.openjdk.jmh:jmh-core:1.24'
    jmhAnnotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.24'

    jmhCompile 'com.google.code.gson:gson:2.8.6'

}

// https://docs.gradle.org/current/dsl/org.gradle.api.tasks.JavaExec.html
task jmh(type: JavaExec, dependsOn: jmhClasses) {
    main = 'zero.x.so.DifferentConcats'
    classpath = sourceSets.jmh.compileClasspath + sourceSets.jmh.runtimeClasspath
    args("version", "$project.version")
}

// to make sure benchmarks always get compiled
classes.finalizedBy(jmhClasses)